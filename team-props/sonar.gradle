subprojects {
    sonarqube {
        skipProject = true
    }
}

configureAndroidModule(':app', 'com.android.application', 'debug')
configureKotlinModule(':base')

def configureAndroidModule(String projectName, String pluginId, String targetVariant) {
    project(projectName) { targetProject ->
        apply plugin: 'jacoco'
        plugins.withId(pluginId) {
            android {
                testOptions {
                    unitTests {
                        includeAndroidResources true
                    }
                }
                afterEvaluate {
                    sonarqube {
                        skipProject = false
                        androidVariant targetVariant

                        properties {
                            property 'sonar.jacoco.reportPaths', [new File(targetProject.buildDir, "jacoco/test${targetVariant.capitalize()}UnitTest.exec").path,
                                                                  'build/outputs/code-coverage/connected/Nexus_5X_API_27_x86(AVD) - 8.1.0-coverage.ec']
                            property 'sonar.junit.reportsPath', new File(targetProject.buildDir, "test-results/test${targetVariant.capitalize()}UnitTest").path
                        }
                    }
                }
            }
        }
    }
}

def configureKotlinModule(String projectName) {
    project(projectName) {
        apply plugin: 'jacoco'
        sonarqube {
            skipProject = false
        }
    }
}
